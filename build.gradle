apply plugin: 'java'
apply plugin: 'application'

mainClassName= 'de.fabianonline.telegram_backup.CommandLineRunner'

repositories {
	mavenCentral()
	maven { 
		url "https://jitpack.io"
	}
}

dependencies {
	compile('com.github.badoualy:kotlogram:0.0.6') {
		exclude module: 'slf4j-simple'
	}
	compile 'org.xerial:sqlite-jdbc:3.16.1'
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile 'com.google.code.gson:gson:2.5'
}

run {
	standardInput = System.in
}

def getVersionName= { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags', '--dirty'
		standardOutput = stdout
	}
	def version = stdout.toString().trim()
	logger.lifecycle("Version $version")
	return version
}

ant.propertyfile(file: "$project.rootDir/build/resources/main/build.properties") {
	entry(key: "version", value: getVersionName())
}

jar {
	manifest {
		attributes "Main-Class": "$mainClassName"
	}
	
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	includes [
		"*.mustache"
	]
}
