apply plugin: 'java'
apply plugin: 'application'

mainClassName= 'de.fabianonline.telegram_backup.CommandLineRunner'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenCentral()
	maven { 
		url "https://jitpack.io"
	}
}

dependencies {
	compile('com.github.badoualy:kotlogram:497e5dd62d9bcb341f584164b04b4b537ce9d295') {
		exclude module: 'slf4j-simple'
	}
	compile 'org.xerial:sqlite-jdbc:3.8.11.2'
	compile 'com.github.spullara.mustache.java:compiler:0.8.18'
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile 'com.google.code.gson:gson:2.5'
	
	testCompile 'junit:junit:4.12'
}

run {
	standardInput = System.in
}

def getVersionName= { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags', '--dirty'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

ant.propertyfile(file: "$project.rootDir/build/resources/main/build.properties") {
	entry(key: "version", value: getVersionName())
}

jar {
	manifest {
		attributes "Main-Class": "$mainClassName"
	}
	
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	includes [
		"*.mustache"
	]
}
